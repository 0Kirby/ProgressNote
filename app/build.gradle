plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 33
    buildToolsVersion = '33.0.2'
    defaultConfig {
        applicationId 'cn.zerokirby.note'
        minSdkVersion 21
        targetSdkVersion 33
        versionCode Integer.valueOf(getDate())
        versionName "3.2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    lint {
        abortOnError false
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a' //select ABIs to build APKs for
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }
    namespace 'cn.zerokirby.note'


    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def aid = variant.applicationId.split("\\.")
            def name = aid[aid.length - 1]
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null) abi = "all"//兼容
            def version = variant.versionName
            def versionCode = variant.versionCode
            def date = new Date()
            def formattedDate = date.format('yyyyMMdd_HHmm')
            outputFileName = "ProgressNote" +
                    "_${abi}" +
                    "_${"v"}${version}" +
                    "_${formattedDate}.apk"

        }
    }
}


static def getDate() {
    return new Date().format("yyyyMMdd")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':theme')
    implementation project(path: ':language')
    implementation project(path: ':base')
    implementation 'com.google.android.material:material:1.9.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.7.0-alpha02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.6'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
